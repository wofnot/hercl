` quicksort.hrc

(b) 0  ` beginning (of items to be sorted)
(m) 1  ` middle
(e) 2  ` end       (of items to be sorted)
(p) 3  ` pivot

[c=2,r=5,s=64,m=64,b=10]

`****************************************************************
`
`****************************************************************
(qsort)                `                    ..(b)(p)
[ (p)>                 `                    ..(b)
   g:!.                `( p > b )?
 | c(b)>c(m)>(e)>v     `  m = b, e = b-1    ..
  (p)<{.>              `  r.=[p]            ..(p)
 |(e)^=1:              `  e++, ( e == p )?
   {.g~!;              `([e] > [p])?
 |(e){(m){(e)}(m)}^1;  ` [m]<->[e], m++
 | !(p){(m){(p)}(m)}   ` [m]<->[p]          ..
  (b)<(m)v<^^<(p)<     `                    ..(b)(m-1)(m+1)(p)
   jj ]                ` [qsort], [qsort]   ..

`****************************************************************
(main)
[  1#c.>c(e)>v         `  r. = b, e = b-1   ..(b)
   i:.                 `  input
 | s~:^};              `  scan items (N)
 | <^<y                `                    ..(b+N)(b)(b+N-1)
  (qsort)j             ` [qsort]            ..(b+N)
 | .=:{^w;             `  write items
 | o ]                 `  output
